{
module Lexer
open Parser
open FSharp.Text.Lexing
open System
open System.Text

let newline (lexbuf: LexBuffer<_>) =
    lexbuf.StartPos <- lexbuf.StartPos.NextLine

let lexeme = LexBuffer<_>.LexemeString

}

let digit = ['0'-'9']
let alpha = ['a'-'z' 'A'-'Z']
let whitespace = [' ' '\t']
let newline = ('\n'|'\r' '\n')
let ident = alpha(alpha|'_'|digit)*
let ignored = '\\' [' ' '\t' '\r' '\n']+ '\\'
let ascii = '\\'(digit)(digit)(digit)
let nonEscape = [ ^ '\n' '\t' '\r' '\"' '\\' ]

rule token = parse
| eof           { EOF }
| whitespace    { token lexbuf }
| newline       { newline lexbuf; token lexbuf }
| "array"       { ARRAY }
| "break"       { BREAK }
| "do"          { DO }
| "else"        { ELSE }
| "end"         { END }
| "for"         { FOR }
| "function"    { FUNCTION }
| "if"          { IF }
| "in"          { IN }
| "let"         { LET }
| "nil"         { NIL }
| "of"          { OF }
| "then"        { THEN }
| "to"          { TO }
| "type"        { TYPE }
| "var"         { VAR }
| "while"       { WHILE }
| "int"         { INT }
| "string"      { STRING }
| ","           { COMMA }
| ":"           { COLON }
| ";"           { SEMICOLON }
| "("           { LPAREN }
| ")"           { RPAREN }
| "["           { LBRACKET }
| "]"           { RBRACKET }
| "{"           { LBRACE }
| "}"           { RBRACE }
| "."           { DOT }
| "+"           { PLUS }
| "-"           { MINUS }
| "*"           { TIMES }
| "/"           { DIV }
| "="           { EQ }
| "<>"          { NE }
| "<"           { LT }
| "<="          { LE }
| ">"           { GT }
| ">="          { GE }
| "&"           { AND }
| "|"           { OR }
| ":="          { ASSIGN }
| ident         { ID (lexeme lexbuf) }
| digit+        { INT_LIT (int <| lexeme lexbuf) }
| "\""          { stringLit (StringBuilder ()) lexbuf }
| "/*"          { comment 0 lexbuf }
and stringLit sb = parse
| ignored       { stringLit sb lexbuf }
| "\\n"         { stringLit (sb.Append('\n')) lexbuf }
| "\\t"         { stringLit (sb.Append('\t')) lexbuf }
| ascii         { stringLit (sb.Append((lexeme lexbuf).[1..] |> int |> char)) lexbuf }
| '\\'          { failwith "Unexpected \\" }
| "\\\""        { stringLit (sb.Append('\"')) lexbuf }
| "\\\\"        { stringLit (sb.Append('\\')) lexbuf }
| nonEscape+    { stringLit (sb.Append(lexeme lexbuf)) lexbuf }
| '\"'          { STRING_LIT (sb.ToString()) }
and comment depth = parse
| "/*"          { comment (depth + 1) lexbuf }
| "*/"          { if depth = 0 then token lexbuf else comment (depth - 1) lexbuf }
| _             { comment depth lexbuf }
